buildscript {
    addRepos(repositories)
    dependencies {
        classpath buildscriptDeps.firebaseCrashlyticsPlugin
        classpath buildscriptDeps.snapshotPublisher
    }
}

apply plugin: 'com.android.application'
apply from: rootProject.file('scripts/versioning.gradle')
apply from: rootProject.file('scripts/read_properties.gradle')
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.kotlin.android.extensions'
//apply plugin: 'androidx.navigation.safeargs.kotlin' TODO enable when compatible

if (getEnvVariable('KEYSTORE_FILE') != null) {
    // Avoid Signing not ready issues in Gradle Play Publisher plugin
    apply from: rootProject.file('scripts/snapshot_publish.gradle')
}

// TODO: Enable when the services json is added
// apply plugin: 'com.google.gms.google-services'

// TODO: Enable Crashlytics
// apply plugin: 'com.google.firebase.crashlytics'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion androidVersions.compileSdk
    buildToolsVersion androidVersions.buildTools

    defaultConfig {
        applicationId "com.xmartlabs.gong"
        minSdkVersion androidVersions.minSdk
        targetSdkVersion androidVersions.targetSdk
        versionCode 1
        versionName ""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        // Used to define some constants like the share preferences's file name.
        buildConfigField 'String', 'APP_NAME', '"Gong"'
    }

    buildFeatures {
        compose true
        viewBinding true
    }

    signingConfigs {
        release {
            storeFile getEnvVariable('KEYSTORE_FILE') == null ? null : file(getEnvVariable('KEYSTORE_FILE'))
            storePassword getEnvVariable('KEYSTORE_PASSWORD')
            keyAlias getEnvVariable('KEYSTORE_KEY_ALIAS')
            keyPassword getEnvVariable('KEYSTORE_KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (getEnvVariable('KEYSTORE_FILE') != null) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            minifyEnabled getEnvVariable('MINIFY_CODE', false) as Boolean
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"
    productFlavors {
        dev {
            buildConfigField 'String', 'API_BASE_URL', getEnvVariable('API_DEV_SERVER_URL')

            dimension "env"
            ext.buildTypeCode = 0
            applicationIdSuffix '.dev'
        }
        prod {
            buildConfigField 'String', 'API_BASE_URL', getEnvVariable('API_PROD_SERVER_URL')

            dimension "env"
            ext.buildTypeCode = 4
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE' // will not include LICENSE file
    }

    lintOptions {
        lintConfig file("lint.xml")
        disable "UnsafeExperimentalUsageError", "UnsafeExperimentalUsageWarning", "ObsoleteLintCustomCheck"
        fatal 'StopShip'
        warningsAsErrors true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
    }

    kotlinOptions {
        useIR = true
        languageVersion = "1.4"
        jvmTarget = '1.8'
        freeCompilerArgs = ["-XXLanguage:+InlineClasses", "-Xopt-in=kotlin.RequiresOptIn", "-Xopt-in=kotlin.time.ExperimentalTime"]
    }

    composeOptions {
        kotlinCompilerVersion versions.kotlin
        kotlinCompilerExtensionVersion versions.compose
    }
}

dependencies {
    androidTestImplementation deps.androidX.test.junit
    androidTestImplementation deps.androidX.test.espresso

    debugImplementation deps.debugDb
    debugImplementation deps.leakcanary

    devImplementation deps.androidSwissKnife.navigationDebug

    implementation deps.androidSwissKnife.core
    implementation deps.androidSwissKnife.navigation
    implementation deps.androidX.appcompat
    implementation deps.androidX.cardview
    implementation deps.androidX.constraint_layout
    implementation deps.androidX.design
    implementation deps.androidX.lifecycle.livedata
    implementation deps.androidX.lifecycle.runtime
    implementation deps.androidX.lifecycle.saveState
    implementation deps.androidX.lifecycle.viewmodel
    implementation deps.androidX.navigation.fragment
    implementation deps.androidX.navigation.ui
    implementation deps.androidX.recyclerview
    implementation deps.androidX.room.coreKtx
    implementation deps.androidX.transition
    implementation deps.androidX.viewPager2
    implementation deps.coil
    implementation deps.compose.compiler
    implementation deps.compose.constraintLayout
    implementation deps.compose.foundation
    implementation deps.compose.liveData
    implementation deps.compose.navigation
    implementation deps.compose.material
    implementation deps.compose.runtime
    implementation deps.compose.tooling
    implementation deps.compose.ui
    implementation deps.compose.viewModel
    implementation deps.firebase.crashlytics
    implementation deps.koin.android
    implementation deps.koin.compose
    implementation deps.kotlin.core
    implementation deps.kotlin.coroutines.core
    implementation deps.kotlin.reflect
    implementation deps.okIo
    implementation deps.okhttp3.core
    implementation deps.okhttp3.loggingIntercepror
    implementation deps.okhttp3.ok2CurlInterceptor
    implementation deps.okhttp3.stetho
    implementation deps.okhttp3.urlconnection
    implementation deps.once
    implementation deps.retrofit2.core
    implementation deps.retrofit2.gsonAdapter
    implementation deps.stetho
    implementation deps.timber

    kapt deps.androidX.room.compiler

    testImplementation deps.junit
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        // Treat all Kotlin warnings as errors
        useIR = true

        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'

        // Enable experimental coroutines APIs, including Flow
        freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi'
        freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.FlowPreview'
        freeCompilerArgs += '-Xopt-in=kotlin.Experimental'
        freeCompilerArgs += '-Xallow-jvm-ir-dependencies'

        // Set JVM target to 1.8
        jvmTarget = "1.8"
    }
}
