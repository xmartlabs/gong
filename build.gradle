// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath libs.gp.androidGradle
        classpath libs.gp.dependencyUpdates
        classpath libs.gp.detekt
        classpath libs.gp.firebaseCrashlytics
        classpath libs.gp.googleServices
        classpath libs.gp.kotlin.gradle
        classpath libs.gp.kotlin.serialization
        classpath libs.gp.snapshotPublisher
    }
}

plugins{
    id 'com.google.devtools.ksp' version '1.9.23-1.0.20' apply false
}

ext {
    // App version
    versionName = '0.0.1' // X.Y.Z; X = Major, Y = minor, Z = Patch level
    moduleName = "Gong"
    name = "Gong"
    configPropertiesFile = file('config.properties') // All configurations are saved here
    secretsPropertiesFile = file('secrets/keys.properties') // All secrets are saved here

    def androidVersions = [:]
    androidVersions.minSdk = 23
    androidVersions.compileSdk = 34
    androidVersions.buildTools = '34.0.0'
    androidVersions.targetSdk = 34
    ext.androidVersions = androidVersions
}


apply from: rootProject.file('scripts/dependency_updates.gradle')
apply from: rootProject.file('scripts/stop_ship_check_xmls.gradle.kts')
apply plugin: "io.gitlab.arturbosch.detekt"

dependencies {
    detektPlugins libs.detekt.formatting
}

detekt {
    autoCorrect = true
    config = files("$rootDir/.detekt/detekt-config.yml")
    input = files("app/src")
    parallel = true
    toolVersion = libs.versions.detekt.get()
}

allprojects {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task check() {
    dependsOn("checkStopShipXmls")
}

// `./gradlew wrapper --gradle-version=[version]` uses the distribution type all
wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
