// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext {
    // App version
    versionName = '0.0.1' // X.Y.Z; X = Major, Y = minor, Z = Patch level
    name = "Gong"
    configPropertiesFile = file('config.properties') // All configurations are saved here
    secretsPropertiesFile = file('secrets/keys.properties') // All secrets are saved here
}

buildscript {
    apply from: 'versions.gradle'
    addRepos(repositories)
    dependencies {
        classpath buildscriptDeps.androidGradle
        classpath buildscriptDeps.dependencyUpdates
        classpath buildscriptDeps.detekt
        classpath buildscriptDeps.googleServices
        classpath buildscriptDeps.kotlin
        classpath buildscriptDeps.kotlinExtensions
    }
}

apply from: rootProject.file('versions.gradle')
apply from: rootProject.file('scripts/dependency_updates.gradle')
apply from: rootProject.file('scripts/stop_ship_check_xmls.gradle.kts')
apply plugin: "io.gitlab.arturbosch.detekt"

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$versions.detekt"
}

detekt {
    autoCorrect = true
    config = files("$rootDir/.detekt/detekt-config.yml")
    input = files("app/src")
    parallel = true
    toolVersion = "$versions.detekt"
}

allprojects {
    apply from: rootProject.file('versions.gradle')
    addRepos(repositories)
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task check() {
    dependsOn("checkStopShipXmls")
}

// `./gradlew wrapper --gradle-version=[version]` uses the distribution type all
wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
